alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }

ident = @{ (alpha | digit | "-" | "/" | "_" | "." )+ }

ctype = @{ ident }
scope = @{ (!("(" | ")") ~ ANY)* }
summary = { (!NEWLINE ~ ANY)* }
break_mark = { "!" }

headline = ${SOI ~  ctype ~ ("(" ~ scope ~ ")" )? ~ break_mark? ~ ": " ~ summary }
text_block = { !trailers ~ NEWLINE{2,} ~ (!NEWLINE{2} ~ ANY)+ }
body = @{ text_block+ }

token = { ident }
value = { (!NEWLINE ~ ANY)* }
colon_trailer = _{ token ~ ":" ~ " "+ ~ value ~ " "* }
hash_trailer = _{ token ~ " "+ ~ "#" ~ ident ~ " "* }
trailer = { colon_trailer | hash_trailer }
trailers = { NEWLINE{2,} ~ trailer ~ (NEWLINE ~ trailer)* ~ NEWLINE? ~ EOI }

message = { headline ~ body? ~ trailers? }
